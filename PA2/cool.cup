/*
 *  cool.cup
 *              Parser definition for the COOL language.
 *
 */

import java_cup.runtime.*;

/* Stuff enclosed in {: :} is copied verbatim to the class containing
all parser actions.  All the extra variables/functions you want to use
in the semantic actions should go here.  Don't remove or modify anything
that was there initially.  */

action code {: 

    int curr_lineno() {
	return ((CoolTokenLexer)parser.getScanner()).curr_lineno();
    }

    AbstractSymbol curr_filename() {
	return ((CoolTokenLexer)parser.getScanner()).curr_filename();
    }
:} 

/************************************************************************/
/*                DONT CHANGE ANYTHING IN THIS SECTION                  */

parser code {:
    int omerrs = 0;

    public void syntax_error(Symbol cur_token) {
        int lineno = action_obj.curr_lineno();
	String filename = action_obj.curr_filename().getString();
        System.err.print("\"" + filename + "\", line " + lineno + 
		         ": parse error at or near ");
        Utilities.printToken(cur_token);
	omerrs++;
	if (omerrs>50) {
	   System.err.println("More than 50 errors");
	   System.exit(1);
	}
    }

    public void unrecovered_syntax_error(Symbol cur_token) {
    }
:}

/* Declare the terminals; a few have types for associated lexemes.  The
token ERROR is never used in the parser; thus, it is a parse error when
the lexer returns it.  */

terminal CLASS, ELSE, FI, IF, IN, INHERITS, LET, LET_STMT, LOOP, POOL, THEN, WHILE;
terminal CASE, ESAC, OF, DARROW, NEW, ISVOID;
terminal ASSIGN, NOT, LE, ERROR;
terminal PLUS, DIV, MINUS, MULT, EQ, LT, DOT, NEG, COMMA, SEMI, COLON;
terminal LPAREN, RPAREN, AT, LBRACE, RBRACE;
terminal AbstractSymbol STR_CONST, INT_CONST;
terminal Boolean BOOL_CONST;
terminal AbstractSymbol TYPEID, OBJECTID;

/*  DON'T CHANGE ANYTHING ABOVE THIS LINE, OR YOUR PARSER WONT WORK       */
/**************************************************************************/

   /* Complete the nonterminal list below, giving a type for the semantic
      value of each non terminal. (See the CUP documentation for details. */

nonterminal programc program;
nonterminal Classes class_list;
nonterminal class_c class;
nonterminal Features feature_list;
nonterminal Feature feature;
nonterminal method Method;
nonterminal attr Attr;
nonterminal Expressions comma_separated_expression_list;
nonterminal Expressions semi_separated_expression_list;
nonterminal Expression expression;
nonterminal Expression let_list;
nonterminal Formals formal_list;
nonterminal Formal formal;
nonterminal Cases case_list;
nonterminal branch Branch;

/* Precedence declarations go here. */
precedence right ASSIGN;
precedence left NOT;
precedence nonassoc LE, LT, EQ;
precedence left PLUS, MINUS;
precedence left MULT, DIV;
precedence left ISVOID;
precedence left NEG;
precedence left AT;
precedence left DOT;

program	
	::= class_list:cl
	    {: RESULT = new programc(curr_lineno(), cl); :}
        ;

class_list
	/* single class */
	::= class:c
	    {: RESULT = (new Classes(curr_lineno())).appendElement(c); :}
	/* several classes */
	| class_list:cl class:c
	    {: RESULT = cl.appendElement(c); :}
	| error
		{: RESULT = new Classes(curr_lineno()); :}
	| class_list:cl error
		{: RESULT = cl; :}
	;

/* If no parent is specified, the class inherits from the Object class */
class
	::= CLASS TYPEID:n LBRACE feature_list:f RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, 
		                   AbstractTable.idtable.addString("Object"), 
				   f, curr_filename()); :}
	| CLASS TYPEID:n INHERITS TYPEID:p LBRACE feature_list:f RBRACE SEMI
	    {: RESULT = new class_c(curr_lineno(), n, p, f, curr_filename()); :}
	;

/* Feature list may be empty, but no empty features in list. */
feature_list
	::= /* empty */
	    {: RESULT = new Features(curr_lineno()); :}
	| feature_list:fl feature:f
		{: RESULT = fl.appendElement(f); :}
	| feature_list:fl error
		{: RESULT = fl; :}
	;

feature
	::= Attr:a
		{: RESULT = a; :}
	| Method:m
		{: RESULT = m; :}
	;

Attr
	::= OBJECTID:o COLON TYPEID:t SEMI
		{: RESULT = new attr(curr_lineno(), o, t, new no_expr(curr_lineno())); :}
	| OBJECTID:o COLON TYPEID:t ASSIGN expression:e SEMI
		{: RESULT = new attr(curr_lineno(), o, t, e); :}
	;

Method
	::= OBJECTID:o LPAREN formal_list:fl RPAREN COLON TYPEID:t LBRACE expression:e RBRACE SEMI
		{: RESULT = new method(curr_lineno(), o, fl, t, e); :}
	;

formal_list
	::=
		{: RESULT = new Formals(curr_lineno()); :}
	| formal:f
		{: RESULT = new Formals(curr_lineno()).appendElement(f); :}
	| formal_list:fl COMMA formal:f
		{: RESULT = fl.appendElement(f); :}
	;

formal
	::= OBJECTID:o COLON TYPEID:t
		{: RESULT = new formalc(curr_lineno(), o, t); :}
	;

comma_separated_expression_list
	::= 
		{: RESULT = new Expressions(curr_lineno()); :}
	| expression:e
		{: RESULT = new Expressions(curr_lineno()).appendElement(e); :}
	| comma_separated_expression_list:el COMMA expression:e
		{: RESULT = el.appendElement(e); :}
	;

semi_separated_expression_list
	::= expression:e SEMI
		{: RESULT = new Expressions(curr_lineno()).appendElement(e); :}
	| semi_separated_expression_list:el expression:e SEMI
		{: RESULT = el.appendElement(e); :}
	| error SEMI
		{: RESULT = new Expressions(curr_lineno()).appendElement(new no_expr(curr_lineno())); :}
	| semi_separated_expression_list:el error SEMI
		{: RESULT = el.appendElement(new no_expr(curr_lineno())); :}
	;

expression
	::= OBJECTID:o ASSIGN expression:e
		{: RESULT = new assign(curr_lineno(), o, e); :}
	| expression:e DOT OBJECTID:o LPAREN comma_separated_expression_list:el RPAREN
		{: RESULT = new dispatch(curr_lineno(), e, o, el); :}
	| OBJECTID:o LPAREN comma_separated_expression_list:el RPAREN
		{: RESULT = new dispatch(curr_lineno(), new object(curr_lineno(), AbstractTable.idtable.addString("self")), o, el); :}
	| expression:e AT TYPEID:t DOT OBJECTID:o LPAREN comma_separated_expression_list:el RPAREN
		{: RESULT = new static_dispatch(curr_lineno(), e, t, o, el); :}
	| IF expression:e1 THEN expression:e2 ELSE expression:e3 FI
		{: RESULT = new cond(curr_lineno(), e1, e2, e3); :}
	| WHILE expression:e1 LOOP expression:e2 POOL
		{: RESULT = new loop(curr_lineno(), e1, e2); :}
	| LBRACE semi_separated_expression_list:el RBRACE
		{: RESULT = new block(curr_lineno(), el); :}
	| LET OBJECTID:o COLON TYPEID:t let_list:ll
		{: RESULT = new let(curr_lineno(), o, t, new no_expr(curr_lineno()), ll); :}
	| LET OBJECTID:o COLON TYPEID:t ASSIGN expression:e1 let_list:ll
		{: RESULT = new let(curr_lineno(), o, t, e1, ll); :}
	| CASE expression:e OF case_list:cl ESAC
		{: RESULT = new typcase(curr_lineno(), e, cl); :}
	| CASE error OF case_list:cl ESAC
		{: RESULT = new typcase(curr_lineno(), new no_expr(curr_lineno()), cl); :}
	| NEW TYPEID:t
		{: RESULT = new new_(curr_lineno(), t); :}
	| ISVOID expression:e
		{: RESULT = new isvoid(curr_lineno(), e); :}
	| expression:e1 PLUS expression:e2
		{: RESULT = new plus(curr_lineno(), e1, e2); :}
	| expression:e1 MINUS expression:e2
		{: RESULT = new sub(curr_lineno(), e1, e2); :}
	| expression:e1 MULT expression:e2
		{: RESULT = new mul(curr_lineno(), e1, e2); :}
	| expression:e1 DIV expression:e2
		{: RESULT = new divide(curr_lineno(), e1, e2); :}
	| NEG expression:e
		{: RESULT = new neg(curr_lineno(), e); :}
	| expression:e1 LT expression:e2
		{: RESULT = new lt(curr_lineno(), e1, e2); :}
	| expression:e1 LE expression:e2
		{: RESULT = new leq(curr_lineno(), e1, e2); :}
	| expression:e1 EQ expression:e2
		{: RESULT = new eq(curr_lineno(), e1, e2); :}
	| NOT expression:e
		{: RESULT = new comp(curr_lineno(), e); :}
	| LPAREN expression:e RPAREN
		{: RESULT = e; :}
	| OBJECTID:o
		{: RESULT = new object(curr_lineno(), o); :}
	| INT_CONST:i
		{: RESULT = new int_const(curr_lineno(), i); :}
	| STR_CONST:s
		{: RESULT = new string_const(curr_lineno(), s); :}
	| BOOL_CONST:b
		{: RESULT = new bool_const(curr_lineno(), b); :}
	;

let_list
	::= IN expression:e
		{: RESULT = e; :}
	| COMMA OBJECTID:o COLON TYPEID:t let_list:ll
		{: RESULT = new let(curr_lineno(), o, t, new no_expr(curr_lineno()), ll); :}
	| COMMA OBJECTID:o COLON TYPEID:t ASSIGN expression:e let_list:ll
		{: RESULT = new let(curr_lineno(), o, t, e, ll); :}
	| error let_list:ll
	;

case_list
	::= Branch:b
	    {: RESULT = (new Cases(curr_lineno())).appendElement(b); :}
	| case_list:cl Branch:b
	    {: RESULT = cl.appendElement(b); :}
	;

Branch
	::= OBJECTID:o COLON TYPEID:t DARROW expression:e SEMI
	    {: RESULT = new branch(curr_lineno(), o, t, e); :}
	;
